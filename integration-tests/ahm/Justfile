# Print help
help:
	@just --list

# Port the Asset Hub Migration Tests to another runtime (Westend or Kusama).
# Will assume runtime.snap and ah-runtime.snap to be present in the root folder.
# Example: just port westend ~polkadot-sdk/cumulus/test/ahm
port RUNTIME SDK FOLDER:
	#!/usr/bin/env bash
	set -e

	echo "Porting integration tests for {{RUNTIME}} to {{FOLDER}}"
	rsync -a -v --ignore-existing ./ {{SDK}}/cumulus/test/ahm/
	rsync -a -v --ignore-existing ../../pallets/rc-migrator {{SDK}}/cumulus/pallets/
	rsync -a -v --ignore-existing ../../pallets/ah-migrator {{SDK}}/cumulus/pallets/
	rsync -a -v --ignore-existing ../../pallets/ah-ops {{SDK}}/cumulus/pallets/

	# Add to workspace
	perl -pi -e 's#members\s*=\s*\[#members = [\n\t"cumulus/pallets/rc-migrator",\n\t"cumulus/pallets/ah-migrator",\n\t"cumulus/test/ahm",\n\t"cumulus/pallets/ah-ops",#' {{SDK}}/Cargo.toml
	perl -pi -e 's#\[workspace.dependencies\]#\[workspace.dependencies\]\npallet-rc-migrator = { path = "cumulus/pallets/rc-migrator" }\npallet-ah-migrator = { path = "cumulus/pallets/ah-migrator" }\npallet-ah-ops = { path = "cumulus/pallets/ah-ops" }#' {{SDK}}/Cargo.toml

	just test-cargo-toml-replaces {{SDK}}/cumulus/test/ahm {{RUNTIME}}
	just test-cargo-toml-replaces {{SDK}}/cumulus/pallets/rc-migrator {{RUNTIME}}
	just test-cargo-toml-replaces {{SDK}}/cumulus/pallets/ah-migrator {{RUNTIME}}
	just test-cargo-toml-replaces {{SDK}}/cumulus/pallets/ah-ops {{RUNTIME}}

	if [ "{{RUNTIME}}" = "westend" ]; then
		just sdk-cargo-toml-replaces {{SDK}}/cumulus/test/ahm
		just sdk-cargo-toml-replaces {{SDK}}/cumulus/pallets/rc-migrator
		just sdk-cargo-toml-replaces {{SDK}}/cumulus/pallets/ah-migrator
		just sdk-cargo-toml-replaces {{SDK}}/cumulus/pallets/ah-ops
		just code-substitute {{SDK}}
	fi

	cd {{FOLDER}}
	cargo fetch
	zepter

	# Actually run the test
	export SKIP_WASM_BUILD=1
	export RUST_BACKTRACE=1
	export SNAP_RC="/Users/vados/{{RUNTIME}}.snap"
	export SNAP_AH="/Users/vados/ah-{{RUNTIME}}.snap"
	export RUST_LOG="warn"

	cargo test -p polkadot-integration-tests-ahm --features "ahm-{{RUNTIME}}" --profile testnet pallet_migration_works -- --nocapture

code-substitute SDK:
	just code-substitute-pallet pallet_proxy {{SDK}}
	just code-substitute-pallet pallet_bounties {{SDK}}
	just code-substitute-pallet pallet_scheduler {{SDK}}
	just code-substitute-pallet pallet_nomination_pools {{SDK}}
	perl -pi -e 's/\+ pallet_referenda::Config</+ pallet_referenda::Config<BlockNumberProvider = frame_system::Pallet<Self>, /' {{SDK}}/cumulus/pallets/rc-migrator/src/lib.rs
	perl -pi -e 's/\+ pallet_referenda::Config</+ pallet_referenda::Config<BlockNumberProvider = <Self as Config>::RcBlockNumberProvider, /' {{SDK}}/cumulus/pallets/ah-migrator/src/lib.rs

code-substitute-pallet PALLET SDK:
	perl -pi -e 's/\+ {{PALLET}}::Config$/+ {{PALLET}}::Config<BlockNumberProvider = frame_system::Pallet<Self>>/' {{SDK}}/cumulus/pallets/rc-migrator/src/lib.rs
	perl -pi -e 's/\+ {{PALLET}}::Config$/+ {{PALLET}}::Config<BlockNumberProvider = <Self as Config>::RcBlockNumberProvider>/' {{SDK}}/cumulus/pallets/ah-migrator/src/lib.rs

test-cargo-toml-replaces FOLDER RUNTIME:
	# Sed differs between Linux and MacOS, so we use the second most cursed tool:
	perl -pi -e 's/asset-hub-polkadot-runtime/asset-hub-{{RUNTIME}}-runtime/' {{FOLDER}}/Cargo.toml
	perl -pi -e 's/^polkadot-runtime /{{RUNTIME}}-runtime /' {{FOLDER}}/Cargo.toml
	perl -pi -e 's/polkadot-runtime-constants/{{RUNTIME}}-runtime-constants/' {{FOLDER}}/Cargo.toml
	# Toggle the default feature
	perl -pi -e 's/"ahm-polkadot"/"ahm-{{RUNTIME}}"/' {{FOLDER}}/Cargo.toml

# The runtimes repo renamed some dependencies that the SDK does not have renamed.
sdk-cargo-toml-replaces FOLDER:
	perl -pi -e 's/authority-discovery-primitives/sp-authority-discovery/' {{FOLDER}}/Cargo.toml
	perl -pi -e 's/babe-primitives/sp-consensus-babe/' {{FOLDER}}/Cargo.toml
	perl -pi -e 's/beefy-primitives/sp-consensus-beefy/' {{FOLDER}}/Cargo.toml
	perl -pi -e 's/grandpa/sp-consensus-grandpa/' {{FOLDER}}/Cargo.toml
	perl -pi -e 's/runtime-parachains/polkadot-runtime-parachains/' {{FOLDER}}/Cargo.toml
